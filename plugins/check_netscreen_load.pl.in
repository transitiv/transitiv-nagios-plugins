#!@PERL@
# vim: et sts=4 sw=4 ts=4
# nagios: +epn
#
# @COPYRIGHT@
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#
# Please send comments, bugs and feature requests to @BUGREPORT@

use strict;
use warnings;

use Net::SNMP qw(:snmp);
use Nagios::Plugin;

BEGIN {
    no warnings 'redefine';
    *Nagios::Plugin::Functions::get_shortname = sub {};
}

sub plugin_die {
    my ($np, $msg) = @_;
    print $np->opts->_usage . "\n";
    print $msg . "\n";
    exit(UNKNOWN);
}

use constant {
    VERSION     => '@VERSION@',
    BLURB       => q{This plugin checks the CPU utilization on Netscreen (now Juniper)
devices that support the NETSCREEN-RESOURCE-MIB.},
    SHORTNAME   => 'Load',
    USAGE       => q{Usage: %s -H <host> -w <WLOAD15m, WLOAD5m,WLOAD1m>
       -c <CLOAD15m,CLOAD5m,CLOAD1m> [-C <community>] [-p <port>]},
    LICENSE     => q{@COPYRIGHT@

This plugin is free software, and comes with ABSOLUTELY NO WARRANTY.
It may be used, redistributed and/or modified under the terms of the
ISC Licence (see http://opensource.org/licenses/isc-license.txt).},
    TIMEOUT     => 10,
};

my $np = Nagios::Plugin->new(
    version     => VERSION,
    blurb       => BLURB,
    shortname   => SHORTNAME,
    usage       => USAGE,
    license     => LICENSE,
    timeout     => TIMEOUT,
);

use constant {
    # NETSCREEN-RESOURCE-MIB::nsResCpuLast1Min
    OID_CPU_1M  => '1.3.6.1.4.1.3224.16.1.2.0',
    # NETSCREEN-RESOURCE-MIB::nsResCpuLast5Min
    OID_CPU_5M  => '1.3.6.1.4.1.3224.16.1.3.0',
    # NETSCREEN-RESOURCE-MIB::nsResCpuLast15Min
    OID_CPU_15M => '1.3.6.1.4.1.3224.16.1.4.0',
};

$np->add_arg(
    spec        => 'host|H=s',
    help        => q{IP address or hostname (use an IP address if possible to prevent a
   DNS query},
    label       => 'ADDRESS',
    required    => 1,
);

$np->add_arg(
    spec        => 'port|p=i',
    help        => 'Port number (default: %s)',
    default     => 161,
);

$np->add_arg(
    spec        => 'community|C=s',
    help        => 'SNMP community string (default: %s)',
    default     => 'public',
);

$np->add_arg(
    spec        => '1',
    help        => 'Use SNMP protocol version 1',
);

$np->add_arg(
    spec        => 'warning|w=s',
    help        => 'Exit with WARNING status if CPU utilization exceeds WLOADn%',
    label       => 'WLOAD15m,WLOAD5m,WLOAD1m',
    required    => 1,
);

$np->add_arg(
    spec        => 'critical|c=s',
    help        => 'Exit with CRITICAL status if CPU utilization exceeds CLOADn%',
    label       => 'CLOAD15m,CLOAD5m,CLOAD1m',
    required    => 1,
);

$np->getopts;

my @warnings = split(/,/, $np->opts->warning);
if (scalar(@warnings) != 3) {
    plugin_die($np, 'Invalid format for warning argument, try --help');
}

my @criticals = split(/,/, $np->opts->critical);
if (scalar(@criticals) != 3) {
    plugin_die($np, 'Invalid format for critical argument, try --help');
}

alarm $np->opts->timeout;

my ($session, $error) = Net::SNMP->session(
    -hostname   => $np->opts->host,
    -version    => $np->opts->{'1'} ? '1' : '2',
    -community  => $np->opts->community,
    -timeout    => $np->opts->timeout/4,
    -retries    => 1,
    -port       => $np->opts->port,
);

unless (defined($session)) {
    alarm 0;
    $np->nagios_die($error);
}

my $result = $session->get_request(
    -varbindlist    => [
        OID_CPU_15M,
        OID_CPU_5M,
        OID_CPU_1M
    ]
);

unless (defined($result)) {
    alarm 0;
    $error = $session->error;
    $session->close();
    $np->nagios_die($error);
}

$session->close();
alarm 0;

if (grep(/^noSuchObject$/i, values(%{$result}))) {
    $np->nagios_die('Host does not support the NETSCREEN-RESOURCE-MIB');
}

my $cpu_15m = $result->{OID_CPU_15M()};
my $cpu_5m = $result->{OID_CPU_5M()};
my $cpu_1m = $result->{OID_CPU_1M()};

my ($code, $message);

$code = $np->check_threshold(
    check       => $cpu_15m,
    warning     => $warnings[0],
    critical    => $criticals[0],
);

$np->add_message(
    $code,
    sprintf("15 minutes: %i%%", $cpu_15m)
);

$code = $np->check_threshold(
    check       => $cpu_5m,
    warning     => $warnings[1],
    critical    => $criticals[1],
);

$np->add_message(
    $code,
    sprintf("5 minutes: %i%%", $cpu_5m)
);

$code = $np->check_threshold(
    check       => $cpu_1m,
    warning     => $warnings[2],
    critical    => $criticals[2],
);

$np->add_message(
    $code,
    sprintf("1 minute: %i%%", $cpu_1m)
);

$np->add_perfdata(
    label       => '15min CPU Utilization',
    value       => $cpu_15m,
    uom         => '%',
    warning     => $warnings[0],
    critical    => $criticals[0],
);

$np->add_perfdata(
    label       => '5min CPU Utilization',
    value       => $cpu_5m,
    uom         => '%',
    warning     => $warnings[1],
    critical    => $criticals[1],
);

$np->add_perfdata(
    label       => '1min CPU Utilization',
    value       => $cpu_1m,
    uom         => '%',
    warning     => $warnings[2],
    critical    => $criticals[2],
);

($code, $message) = $np->check_messages(
    join    => ', '
);

$np->nagios_exit($code, $message);
