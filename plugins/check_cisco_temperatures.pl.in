#!@PERL@ -w
# vim: et sts=4 sw=4 ts=4
# nagios: +epn
#
# @COPYRIGHT@
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#
# Please send comments, bugs and feature requests to @BUGREPORT@

use strict;
use Net::SNMP qw(:snmp);
use Nagios::Plugin;

use constant {
    VERSION     => '@VERSION@',
    BLURB       => q{This plugin checks the temperature readings of Cisco devices that
support the CISCO-ENVMON-MIB. Currently only SNMPv2 is supported.},
    SHORTNAME   => 'Temperatures',
    USAGE       => q{Usage: %s -H <host> [-C <community>] [-i] [-t <timeout>]},
    LICENSE     => q{@COPYRIGHT@

This plugin is free software, and comes with ABSOLUTELY NO WARRANTY.
It may be used, redistributed and/or modified under the terms of the
ISC Licence (see http://opensource.org/licenses/isc-license.txt).},
    TIMEOUT     => 10,
};

# CISCO-ENVMON-MIB::ciscoEnvMonTemperatureStatusTable
my $status_table_oid = ".1.3.6.1.4.1.9.9.13.1.3.1";
# CISCO-ENVMON-MIB::ciscoEnvMonTemperatureStatusDescr
my $desc_oid = ".1.3.6.1.4.1.9.9.13.1.3.1.2";
# CISCO-ENVMON-MIB::ciscoEnvMonTemperatureStatusValue
my $status_oid = ".1.3.6.1.4.1.9.9.13.1.3.1.3";
# CISCO-ENVMON-MIB::ciscoEnvMonTemperatureThreshold
my $threshold_oid = ".1.3.6.1.4.1.9.9.13.1.3.1.4";
# CISCO-ENVMON-MIB::ciscoEnvMonTemperatureState
my $state_oid = ".1.3.6.1.4.1.9.9.13.1.3.1.6";

# Represents the state of a device being monitored.
# Valid values are:
#
# normal(1):         the environment is good, such as low
#                    temperature.
#
# warning(2):        the environment is bad, such as temperature
#                    above normal operation range but not too
#                    high.
#
# critical(3):       the environment is very bad, such as
#                    temperature much higher than normal
#                    operation limit.
#
# shutdown(4):       the environment is the worst, the system
#                    should be shutdown immediately.
#
# notPresent(5):     the environmental monitor is not present,
#                    such as temperature sensors do not exist.
#
# notFunctioning(6): the environmental monitor does not 
#                    function properly, such as a temperature
#                    sensor generates a abnormal data like
#                    1000 C.

use constant {
    SENSOR_NORMAL           => 1,
    SENSOR_WARNING          => 2,
    SENSOR_CRITICAL         => 3,
    SENSOR_SHUTDOWN         => 4,
    SENSOR_NOTPRESENT       => 5,
    SENSOR_NOTFUNCTIONING   => 6,
};

my $np = Nagios::Plugin->new(
    version     => VERSION,
    blurb       => BLURB,
    shortname   => SHORTNAME,
    usage       => USAGE,
    license     => LICENSE,
    timeout     => TIMEOUT,
);

$np->add_arg(
    spec        => 'host|H=s',
    help        => q{IP address or hostname (use an IP address if possible to prevent a
   DNS query)},
    label       => 'ADDRESS',
    required    => 1,
);

$np->add_arg(
    spec        => 'community|C=s',
    help        => 'SNMP community string (default: %s)',
    default     => 'public',
);

$np->add_arg(
    spec        => 'ignore-non-functional|i',
    help        => q{Do not exit with CRITICAL status when sensors are reported as
   non-functional},
);

$np->getopts;

alarm $np->opts->timeout;

my ($session, $error) = Net::SNMP->session(
    -hostname   => $np->opts->host,
    -version    => '2',
    -community  => $np->opts->community,
    -timeout    => $np->opts->timeout/4,
    -port       => 161,
    -retries    => 1,
);

if (! defined($session)) {
    alarm 0;
    $np->nagios_die($error);
}

my $result = $session->get_table(
    -baseoid    => $status_table_oid,
);

if (!defined($result)) {
    alarm 0;
    $error = $session->error;
    $session->close();
    $np->nagios_die($error);
}

alarm 0;

my (@descs, @states, @statuses, @thresholds);

foreach my $key (oid_lex_sort(keys %$result)) {
    if (oid_base_match($state_oid, $key)) {
        push(@states, $$result{$key});
    } elsif (oid_base_match($desc_oid, $key)) {
        push(@descs, $$result{$key});
    } elsif (oid_base_match($status_oid, $key)) {
        push(@statuses, $$result{$key});
    } elsif (oid_base_match($threshold_oid, $key)) {
        push(@thresholds, $$result{$key});
    }
}

my $len = scalar(@descs);
my $ok_cnt = 0;

for (my $i = 0; $i < $len; ++$i) {
    if ($states[$i] == SENSOR_NORMAL) {
        ++$ok_cnt;
    } elsif ($states[$i] == SENSOR_WARNING) {
        $np->add_message(WARNING, $descs[$i] . ": warning");
    } elsif ($states[$i] == SENSOR_CRITICAL || $states[$i] == SENSOR_SHUTDOWN) {
        $np->add_message(CRITICAL,
            $descs[$i] . ": " . ($states[$i] == 3 ? "critical" : "shutdown")
        );
    } elsif ($states[$i] == SENSOR_NOTFUNCTIONING) {
        if ($np->opts->{'ignore-non-functional'}) {
            ++$ok_cnt;
        } else {
            $np->add_message(CRITICAL, $descs[$i] . ": non-functional");
        }
    }
    
    # Some devices don't report temperature values/thresholds
    if (defined($statuses[$i]) && defined($thresholds[$i])) {
        $np->add_perfdata(
            label       => $descs[$i],
            value       => $statuses[$i],
            critical    => $thresholds[$i],
        );
    }

}

if ($ok_cnt > 0) {
    if ($ok_cnt == $len) {
        $np->add_message(OK, "All sensors normal");
    } else {
        $np->add_message(OK, "$ok_cnt/$len sensors in normal state");
    }
} else {
    $np->nagios_die("No temperature sensors found!");
}

my ($code, $message) = $np->check_messages(join => ', ');
$np->nagios_exit($code, $message);
